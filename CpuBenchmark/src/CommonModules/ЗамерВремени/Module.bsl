
#Область ПрограммныйИнтерфейс

// Инициализировать данные замера.
// 
// Возвращаемое значение:
//  ТаблицаЗначений - Инициализировать данные замера:
// * Наименование - Строка - Наименование замера
// * Начало - Число - Начало замера в миллисекундах
// * Конец - Число - Окончание замера в миллисекундах
// * КоличествоЭлементов - Число - Количество итераций в замере
// * Длительность - Число - Длительность замера в миллисекундах   
Функция ИнициализироватьДанныеЗамера() Экспорт
	
	ОписаниеЧисла  = Новый ОписаниеТипов("Число",Новый КвалификаторыЧисла(15, 0));
	ОписаниеСтрока = Новый ОписаниеТипов("Строка",,Новый КвалификаторыСтроки(25));
	
	ДанныеЗамера = Новый ТаблицаЗначений();
	ДанныеЗамера.Колонки.Добавить("Наименование",ОписаниеСтрока);
	ДанныеЗамера.Колонки.Добавить("Начало", ОписаниеЧисла);
	ДанныеЗамера.Колонки.Добавить("Конец", ОписаниеЧисла);
	ДанныеЗамера.Колонки.Добавить("КоличествоЭлементов", ОписаниеЧисла);
	ДанныеЗамера.Колонки.Добавить("Длительность", ОписаниеЧисла);
		
	Возврат ДанныеЗамера;
КонецФункции

// Начать замер.
// 
// Параметры:
//  ДанныеЗамера - См. ИнициализироватьДанныеЗамера
//  НаименованиеЗамера - Строка - Наименование замера
//  КоличествоЭлементов - Число - Количество элементов
// 
// Возвращаемое значение:
//  СтрокаТаблицыЗначений - Начать замер:
// * Наименование - Строка - Наименование замера//  
// * Начало - Число - Начало замера в миллисекундах
// * Конец - Число - Окончание замера в миллисекундах
// * КоличествоЭлементов - Число - Количество итераций в замере
// * Длительность - Число - Длительность замера в миллисекундах
Функция НачатьЗамер(ДанныеЗамера, Знач НаименованиеЗамера, Знач КоличествоЭлементов = 0) Экспорт
			
	Замер = ДанныеЗамера.Добавить();
	Замер.Наименование = НаименованиеЗамера;
	Замер.Начало = ТекущаяУниверсальнаяДатаВМиллисекундах();
	Замер.КоличествоЭлементов = КоличествоЭлементов;
	
	Возврат Замер;
			
КонецФункции

// Завершить замер.
// 
// Параметры:
//  Замер - См. НачатьЗамер
//  КоличествоЭлементов - Число - Количество элементов замера
Процедура ЗавершитьЗамер(Замер, Знач КоличествоЭлементов = 0) Экспорт

	Если Замер = Неопределено Тогда
		Возврат;
	КонецЕсли;
			
	Если КоличествоЭлементов > 0 Тогда
		Замер.КоличествоЭлементов = КоличествоЭлементов;
	КонецЕсли;
		
	Замер.Конец = ТекущаяУниверсальнаяДатаВМиллисекундах();
	Замер.Длительность = Замер.Конец - Замер.Начало;
	
КонецПроцедуры

#КонецОбласти
